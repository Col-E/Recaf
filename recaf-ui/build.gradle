plugins {
    id 'application'
    alias(libs.plugins.javafx)
    alias(libs.plugins.shadow)
}

def javaFxVersion = '23.0.2'
def javaFxIncludeInDist = System.getProperty('skip_jfx_bundle') == null

javafx {
    version = javaFxVersion
    modules = ['javafx.controls', 'javafx.media']
}

dependencies {
    implementation project(':recaf-core')
    testImplementation(testFixtures(project(":recaf-core")))

    implementation(libs.acc.agent)
    implementation(libs.atlantafx)
    implementation(libs.docking)
    implementation(libs.bundles.ikonli)
    implementation(libs.bundles.image.io)
    implementation(libs.jsvg)
    implementation(libs.reactfx)
    implementation(libs.richtextfx)
    implementation(libs.treemapfx)
}

application {
    mainClass = 'software.coley.recaf.Main'
}

jar {
    manifest {
        attributes(
                'Enable-Native-Access': 'ALL-UNNAMED',
                'Main-Class': 'software.coley.recaf.Main'
        )
    }
}

shadowJar {
    exclude "META-INF/maven/**"
    exclude "META-INF/rewrite/**"
    exclude "META-INF/proguard/*"
    exclude "META-INF/plugins/*"
    exclude "META-INF/native-image/*"
    exclude "META-INF/*.properties"

    dependencies {
        exclude(dependency(javaFxIncludeInDist ? 'invalid:invalid:invalid' : 'org.openjfx:.*:.*'))
    }
}