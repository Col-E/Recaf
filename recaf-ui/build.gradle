plugins {
    id 'application'
    alias(libs.plugins.javafx)
    alias(libs.plugins.shadow)
}

def javaFxVersion = '22.0.1'
def javaFxIncludeInDist = System.getProperty('skip_jfx_bundle') == null

javafx {
    version = javaFxVersion
    modules = ['javafx.controls', 'javafx.media']
}

dependencies {
    implementation project(':recaf-core')
    testImplementation(testFixtures(project(":recaf-core")))

    implementation(libs.atlantafx)
    implementation(libs.docking)
    implementation(libs.bundles.ikonli)
    implementation(libs.bundles.image.io)
    implementation(libs.jsvg)
    implementation(libs.reactfx)
    implementation(libs.richtextfx)
    implementation(libs.treemapfx)
    implementation(libs.jgraphx)
    implementation(libs.fxgraphics2d)
}

application {
    mainClass = 'software.coley.recaf.Main'
}

shadowJar {
    exclude "META-INF/maven/**"
    exclude "META-INF/rewrite/**"
    exclude "META-INF/proguard/*"
    exclude "META-INF/plugins/*"
    exclude "META-INF/native-image/*"
    exclude "META-INF/*.properties"

    dependencies {
        exclude(dependency(javaFxIncludeInDist ? 'invalid:invalid:invalid' : 'org.openjfx:.*:.*'))
    }
}