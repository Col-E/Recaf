import com.github.benmanes.gradle.versions.VersionsPlugin

plugins {
    id "com.github.ben-manes.versions" version "0.39.0"
}

allprojects {
    group 'me.coley'
    version '3.0.0-SNAPSHOT'
}

if (!JavaVersion.current().isJava11Compatible()) {
    throw new IllegalStateException('JDK 11+ is required to build Recaf')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: VersionsPlugin

    checkstyleMain.exclude '**/generated-src/**'

    sourceCompatibility = 11
    targetCompatibility = 11
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.incremental = true
    }

    repositories {
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://jitpack.io' }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    project.ext {
        def asmVersion = '9.2'
        def junitVersion = '5.7.0'
        def dexVersion = '2.5.2'
        def jpVersion = '3.23.1'
        def dudeVersion = '1.9.4'
        asm = "org.ow2.asm:asm:$asmVersion"
        asm_analysis = "org.ow2.asm:asm-analysis:$asmVersion"
        asm_commons = "org.ow2.asm:asm-commons:$asmVersion"
        asm_tree = "org.ow2.asm:asm-tree:$asmVersion"
        asm_util = "org.ow2.asm:asm-util:$asmVersion"
        awaitility = "org.awaitility:awaitility:4.1.0"
        baksmali = "org.smali:baksmali:$dexVersion"
        cafedude = "com.github.Col-E:CAFED00D:$dudeVersion"
        common_io = 'commons-io:commons-io:2.9.0'
        cfr = 'org.benf:cfr:0.152'
        dexlib = "org.smali:dexlib2:$dexVersion"
        directories = 'dev.dirs:directories:20'
        gson = 'com.google.code.gson:gson:2.8.7'
        guava = 'com.google.guava:guava:30.1-jre'
        hppc = 'com.carrotsearch:hppc:0.9.0'
        jadx = 'io.github.skylot:jadx-core:1.3.2'
        jadx_input = 'io.github.skylot:jadx-java-input:1.3.2'
        java_parser = "com.github.javaparser:javaparser-symbol-solver-core:$jpVersion"
        javax_annos = 'javax.annotation:javax.annotation-api:1.3.2'
        jphantom = 'com.github.Col-E:jphantom:1.4.3'
        junit_api = "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        junit_engine = "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        junit_params = "org.junit.jupiter:junit-jupiter-params:$junitVersion"
        logback_classic = 'ch.qos.logback:logback-classic:1.2.3'
        pe = 'com.github.cademtz:JavaPeParser:1.0.0'
        javassist = 'org.javassist:javassist:3.28.0-GA'
        jelf = 'net.fornwall:jelf:0.7.0'
        picocli = 'info.picocli:picocli:4.6.1'
        regex = 'net.sourceforge.jregex:jregex:1.2_01'
        richtextfx = 'org.fxmisc.richtext:richtextfx:0.10.6'
        smali = "org.smali:smali:$dexVersion"
    }
}
