apply plugin: 'java-library'
apply plugin: 'antlr'

group 'me.coley'

repositories {
    mavenCentral()
}

dependencies {
    api project(':recaf-utils')

    def antlrVersion = '4.9.3'
    def jasmVersion = '1.1.1'
    antlr "org.antlr:antlr4:$antlrVersion"
    api "org.antlr:antlr4-runtime:$antlrVersion"

    api asm
    api asm_tree

    api "me.darknet:Jasm:$jasmVersion"

    // Testing
    testImplementation asm_util
    testImplementation junit_api
    testImplementation junit_params
    testRuntimeOnly junit_engine
}

generateGrammarSource {
    // The "-lib <path>" args are requires for the ANTLR plugin to support "import" usage.
    def packageName = "me.coley.recaf.assemble.parser"
    def dirName = "src/main/antlr/" + packageName.replace('.', '/');
    maxHeapSize = "128m"
    arguments += ['-package', packageName, '-visitor', '-no-listener', '-lib', dirName]
}
compileJava.dependsOn generateGrammarSource

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
