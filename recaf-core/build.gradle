plugins {
    id "com.peterabeles.gversion" version "1.10"
}

apply plugin: 'java-library'

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations {
    testSourceOutput
}

dependencies {
    api project(':recaf-assembler')
    api project(':recaf-plugin')
    api project(':recaf-jdk-tools')

    api fileTree(dir: 'libs', include: ['*.jar'])

    api asm
    api asm_analysis
    api asm_commons
    api asm_tree
    api asm_util
    api baksmali
    api cdi
    api cfr
    api common_io
    api dexlib
    api diff
    api directories
    api gson
    api instrument_server
    api (jadx) { exclude group: 'com.android.tools.build' }
    api jadx_input
    api jandex
    api java_parser
    api javax_annos
    api jphantom
    api logback_classic
    api picocli
    api quiltflower
    api ssvm
    api procyon_core
    api procyon_compiler_tools

    // Android
    api smali
    api aapt

    // Testing
    // Export test class, has been accessed by other modules
    testSourceOutput sourceSets.test.output
    testImplementation junit_api
    testImplementation junit_params
    testRuntimeOnly junit_engine
}

test {
    useJUnitPlatform()
}

checkstyle {
    checkstyleTest.enabled = false
}

jar {
    manifest.attributes('Specification-Version': project.version)
}

sourceSets {
    main {
        java {
            srcDirs 'src/generated/java', 'src/main/java'
        }
    }
}

gversion {
    srcDir       = "src/generated/java/"
    classPackage = "me.coley.recaf"
    className    = "BuildConfig"
    dateFormat   = "yyyy MM/dd HH:mm"
    debug        = true
    language     = "java"
    explicitType = false
    annotate     = true
}

// Uncomment this line to ensure the version file is generated each compilation
project.compileJava.dependsOn(createVersionFile)
