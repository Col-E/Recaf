import java.nio.file.Files

plugins {
    alias(libs.plugins.peterabeles.gversion)
}

apply plugin: 'java-library'
apply plugin: 'java-test-fixtures'

dependencies {
    api(libs.bundles.asm)
    api(libs.binary.resources)
    api(libs.cafedude)
    api(libs.bundles.cdi)
    api(libs.cfr)
    api(libs.dex.translator) {
        exclude group: 'com.android.tools'
    }
    api(libs.diffutils)
    api(libs.bundles.downgrader) {
        exclude group: 'org.ow2.asm'
    }
    api(libs.extra.collections)
    api(libs.extra.observables)
    api(libs.gson) // required by R8 (from dex-translator)
    api(libs.instrument.server)
    api(libs.jelf)
    api(libs.jphantom)
    api(libs.lljzip)
    api(libs.bundles.logging)
    api(libs.mapping.io)
    api(libs.natural.order)
    api(libs.pe)
    api(libs.picocli)
    api(libs.procyon)
    api(libs.jlinker)
    api(libs.regex)
    api(libs.bundles.jasm)
    api(libs.vineflower)
    api(libs.sourcesolver)

    // We use Jackson as a test dependency so that we can convert Android API XML models into JSON.
    // This way, we only need to include GSON in the final Recaf build.
    testImplementation(libs.jackson)
}

// Force generation of gversion data class when the version information is not up-to-date
tasks.register('conditionalBuildConfigUpdate') {
    if (!isBuildConfigUpToDate()) {
        finalizedBy createVersionFile
    }
}

project.compileJava.dependsOn('conditionalBuildConfigUpdate')

gversion {
    srcDir = "src/generated/java/"
    classPackage = "software.coley.recaf"
    className = "RecafBuildConfig"
    dateFormat = "yyyy MM/dd HH:mm"
    debug = true
    language = "java"
    explicitType = false
    annotate = false
}

sourceSets {
    // Need to add the generated class to the source set
    main {
        java {
            srcDirs 'src/generated/java', 'src/main/java'
        }
    }
}

clean {
    // Delete the generated gversion class when cleaning
    delete 'src/generated'
}

private boolean isBuildConfigUpToDate() {
    File buildConfigPath = project.file(gversion.srcDir +
            gversion.classPackage.replace('.', '/') + '/' +
            gversion.className + ".java")
    if (buildConfigPath.exists()) {
        String text = Files.readString(buildConfigPath.toPath())
        if (text.contains('VERSION = "' + project.version + '"'))
            return true
    }
    return false
}